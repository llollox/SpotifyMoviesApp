apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.google.protobuf'
apply from: '../dependencies.gradle'

sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}

android {
    compileSdkVersion versions.sdk.compile


    defaultConfig {
        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation dependency.networking.values()
    implementation dependency.ui.values()
    implementation dependency.image.values()
    api dependency.architectureComponents.values()
    implementation dependency.dependencyInjection.values()
    implementation dependency.util.values()
    annotationProcessor dependency.dependencyInjectionProcessor.values()
    annotationProcessor dependency.dataBase.roomCompiler

    testImplementation dependency.testing.values()
    androidTestImplementation dependency.testingAndroid.values()
    debugImplementation dependency.debug.values()

    implementation 'com.squareup.retrofit2:converter-protobuf:2.6.2'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
    protobuf 'com.google.protobuf:protobuf-java:3.7.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}
